import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.3.11"
    id "com.github.johnrengelman.shadow" version '4.0.3'
}

// jitpack
group = 'com.github.kotlin-graphics'

ext{
    moduleName = 'com.github.kotlin-graphics.imgui'
    kotlin = 'org.jetbrains.kotlin:kotlin'
    kotlin_version = '1.3.11'
    kotlintest_version = '3.2.0'
    lwjgl_version = "3.2.1"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kotlin-stdlib"

    implementation 'com.github.kotlin-graphics:uno-sdk:dbc2c26611b51f0be9e3d58d56ff6c7e6b87d58b'

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version".toString()

    constraints {
        testImplementation("$kotlin-stdlib:$kotlin_version")
        testImplementation("$kotlin-stdlib-jdk7:$kotlin_version")
        testImplementation("$kotlin-reflect:$kotlin_version")
    }

    def joglVersion = '2.3.2'
    implementation "org.jogamp.gluegen:gluegen-rt-main:$joglVersion"
    implementation "org.jogamp.jogl:jogl-all-main:$joglVersion"

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb"].each {
        String base = "org.lwjgl:lwjgl$it:$lwjgl_version"
        implementation base
        testRuntime "$base:natives-$lwjgl_natives"
        shadow "$base:natives-$lwjgl_natives"
    }

    compile files("${System.properties['java.home']}/../lib/tools.jar") // for debug, classes instances
}

repositories {
    mavenCentral()
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { url 'https://jitpack.io' }
}

task lightJar(type: Jar) {
    classifier = 'light'
    from sourceSets.main.output
    exclude 'extraFonts'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives lightJar
    archives sourcesJar
    archives javadocJar
}

shadowJar {
    exclude 'module-info.class'
    inputs.property("moduleName", moduleName)
    manifest.attributes('Automatic-Module-Name': moduleName)
}

jar {
    inputs.property("moduleName", moduleName)
    manifest.attributes('Automatic-Module-Name': moduleName)
}